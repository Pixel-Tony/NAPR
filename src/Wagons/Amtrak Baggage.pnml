//--------------------------------------------------------------------------------------------------Amtrak Baggage
spriteset (spriteset_Amtrak_Baggage_purchase_Phase_III, "gfx/USA_Amtrak_Baggage.png") { [4,  97,   87,  12,  -15,  -9] }
spriteset (spriteset_Amtrak_Baggage_purchase_Phase_IVa, "gfx/USA_Amtrak_Baggage.png") { [92, 97,   87,  12,  -15,  -9] }
spriteset (spriteset_Amtrak_Baggage_purchase_Phase_V, "gfx/USA_Amtrak_Baggage.png")   { [4,  116,  87,  12,  -15,  -9] }

template tmpl_Amtrak_Baggage(x,y) {
      [x,      y,    8,     24,     -3,    -12]
    [x+9,    y,    22,    20,     -14,   -11]
    [x+32,   y,    32,    16,     -16,   -12]
    [x+65,   y,    22,    20,     -8,    -12]
}

spriteset (spriteset_Amtrak_Baggage_Phase_III, "gfx/USA_Amtrak_Baggage.png") { tmpl_Amtrak_Baggage(4,4)  tmpl_Amtrak_Baggage(92,4) }
spriteset (spriteset_Amtrak_Baggage_Phase_IVa, "gfx/USA_Amtrak_Baggage.png") { tmpl_Amtrak_Baggage(4,35) tmpl_Amtrak_Baggage(92,35) }
spriteset (spriteset_Amtrak_Baggage_Phase_V, "gfx/USA_Amtrak_Baggage.png")   { tmpl_Amtrak_Baggage(4,66) tmpl_Amtrak_Baggage(92,66) }

switch(FEAT_TRAINS, SELF, sw_Amtrak_Baggage_purchase, current_year) {
    0..1992:  spriteset_Amtrak_Baggage_purchase_Phase_III;
    1993..1999:  spriteset_Amtrak_Baggage_purchase_Phase_IVa;
    spriteset_Amtrak_Baggage_purchase_Phase_V;
}
switch(FEAT_TRAINS, SELF, sw_Amtrak_Baggage_year_by_year, current_year) {
    0..1992:  spriteset_Amtrak_Baggage_Phase_III;
    1993..1999:  spriteset_Amtrak_Baggage_Phase_IVa;
    spriteset_Amtrak_Baggage_Phase_V;
}
switch(FEAT_TRAINS, SELF, sw_Amtrak_Baggage_livery, cargo_subtype) {
    0:  spriteset_Amtrak_Baggage_Phase_III;
    1:  spriteset_Amtrak_Baggage_Phase_IVa;
    2:  spriteset_Amtrak_Baggage_Phase_V;
    sw_Amtrak_Baggage_year_by_year;
}
switch(FEAT_TRAINS, SELF, sw_Amtrak_Baggage_livery_name, cargo_subtype) {
    0:  string(STR_Amtrak_Phase_III);
    1:  string(STR_Amtrak_Phase_IV);
    2:  string(STR_Amtrak_Phase_V);
    string(STR_Livery_Auto);
}

item(FEAT_TRAINS, item_Amtrak_Baggage, 702) {
    property {
        name:                         string(STR_Amtrak_Baggage_Name);
        climates_available:           bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
        introduction_date:            date(1976, 1, 1);
        model_life:                   VEHICLE_NEVER_EXPIRES;
        vehicle_life:                 20;
        refittable_cargo_classes:     bitmask(CC_MAIL);
        non_refittable_cargo_classes: bitmask();
        loading_speed:                18;
        cost_factor:                  140;
        running_cost_factor:          1;
        sprite_id:                    SPRITE_ID_NEW_TRAIN;
        speed:                        180 km/h;
        misc_flags:                   bitmask();
        refit_cost:                   0;
        track_type:                   RAIL;
        ai_special_flag:              AI_FLAG_PASSENGER;
        power:                        0;
        cargo_capacity:               120;
        weight:                       58 ton;
        ai_engine_rank:               0;
        tractive_effort_coefficient:  0.3;
        air_drag_coefficient:         0.06;
        visual_effect_and_powered:    visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
        extra_weight_per_wagon:       0;
        bitmask_vehicle_info:         0;
    }
    graphics {
        purchase:                 sw_Amtrak_Baggage_purchase;
        additional_text:          return string(STR_Obsolete_Desc);
        length:                   8;
        cargo_subtype_text:       sw_Amtrak_Baggage_livery_name;
        default:                  sw_Amtrak_Baggage_livery;
    }
}
