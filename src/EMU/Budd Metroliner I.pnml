/* * * * * * * * * *
 * Budd Metroliner I
 */
spriteset (spriteset_purchase_Metroliner_I, "gfx/USA_Amtrak_Metroliner_I.png") { [4,  136,  95,  16,  -27,  -12] }

template tmpl_Metroliner_I(x,y) {
    [x,      y,  8, 26,  -3,  -9]
    [x + 9,  y, 24, 21, -16, -10]
    [x + 34, y, 36, 16, -20, -12]
    [x + 71, y, 24, 21, -10, -12]
}

template tmpl_Metroliner_I_second(x,y) {
    [x,      y,  8, 26,  -3, -11]
    [x + 9,  y, 24, 21, -14, -11]
    [x + 34, y, 36, 16, -16, -12]
    [x + 71, y, 24, 21,  -8, -11]
}

spriteset(spriteset_Metroliner_I_f_yellow, "gfx/USA_Amtrak_Metroliner_I.png") { tmpl_Metroliner_I(4,4)     tmpl_Metroliner_I_second(100,4) }
spriteset(spriteset_Metroliner_I_f_none, "gfx/USA_Amtrak_Metroliner_I.png") { tmpl_Metroliner_I(4,37)    tmpl_Metroliner_I_second(100,37) }
spriteset(spriteset_Metroliner_I_b_none, "gfx/USA_Amtrak_Metroliner_I.png") { tmpl_Metroliner_I(100,70)  tmpl_Metroliner_I_second(4,70) }
spriteset(spriteset_Metroliner_I_b_red, "gfx/USA_Amtrak_Metroliner_I.png") { tmpl_Metroliner_I(100,103) tmpl_Metroliner_I_second(4,103) }

spriteset(spriteset_Metroliner_I_f_yellow_PC, "gfx/USA_PC_Metroliner_I.png") { tmpl_Metroliner_I(4,4)     tmpl_Metroliner_I_second(100,4) }
spriteset(spriteset_Metroliner_I_f_none_PC, "gfx/USA_PC_Metroliner_I.png") { tmpl_Metroliner_I(4,37)    tmpl_Metroliner_I_second(100,37) }
spriteset(spriteset_Metroliner_I_b_none_PC, "gfx/USA_PC_Metroliner_I.png") { tmpl_Metroliner_I(100,70)  tmpl_Metroliner_I_second(4,70) }
spriteset(spriteset_Metroliner_I_b_red_PC, "gfx/USA_PC_Metroliner_I.png") { tmpl_Metroliner_I(100,103) tmpl_Metroliner_I_second(4,103) }

switch(FEAT_TRAINS, SELF, sw_Metroliner_I_f_y, cargo_subtype) {
    0: spriteset_Metroliner_I_f_yellow;
    spriteset_Metroliner_I_f_yellow_PC;
}
switch(FEAT_TRAINS, SELF, sw_Metroliner_I_f_n, cargo_subtype) {
    0: spriteset_Metroliner_I_f_none;
    spriteset_Metroliner_I_f_none_PC;
}
switch(FEAT_TRAINS, SELF, sw_Metroliner_I_b_n, cargo_subtype) {
    0: spriteset_Metroliner_I_b_none;
    spriteset_Metroliner_I_b_none_PC;
}
switch(FEAT_TRAINS, SELF, sw_Metroliner_I_b_r, cargo_subtype) {
    0: spriteset_Metroliner_I_b_red;
    spriteset_Metroliner_I_b_red_PC;
}

switch(FEAT_TRAINS, SELF, sw_Metroliner_I_articulated_part, extra_callback_info1) {
    1..5: item_Metroliner_I;
    CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch(FEAT_TRAINS, SELF, sw_Metroliner_I_grapfics4, position_in_vehid_chain % 6) {
    0: sw_Metroliner_I_f_n;
    3: sw_Metroliner_I_b_n;
    spriteset_obsolete; // is never returned, see first switch in chain
}
switch(FEAT_TRAINS, SELF, sw_Metroliner_I_grapfics3, position_in_vehid_chain_from_end) {
    2: sw_Metroliner_I_b_r;
    sw_Metroliner_I_grapfics4;
}
switch(FEAT_TRAINS, SELF, sw_Metroliner_I_grapfics2, position_in_vehid_chain) {
    0: sw_Metroliner_I_f_y;
    sw_Metroliner_I_grapfics3;
}
switch(FEAT_TRAINS, SELF, sw_Metroliner_I_grapfics, position_in_vehid_chain % 3) {
    0: sw_Metroliner_I_grapfics2;
    spriteset_obsolete;
}

switch(FEAT_TRAINS,SELF, sw_Metroliner_I_livery, cargo_subtype) {
    0: string(STR_Livery_AMTR);
    string(STR_Livery_Penn_Central);
}

switch(FEAT_TRAINS, SELF, sw_Metroliner_I_capacity, position_in_vehid_chain % 3) {
    0: 115;
    0;
}

switch(FEAT_TRAINS, SELF, sw_Metroliner_I_length, position_in_vehid_chain % 3) {
    1: 1;
    4;
}

item(FEAT_TRAINS, item_Metroliner_I, 301) {
    property {
        name:                           string(STR_Metroliner_I_Name);
        climates_available:             bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
        introduction_date:              date(1969, 1, 16);
        model_life:                     20;
        vehicle_life:                   30;
        reliability_decay:              12;
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   bitmask();
        loading_speed:                  5;
        cost_factor:                    32;
        running_cost_factor:            32;
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          180 km/h;
        misc_flags:                     bitmask(TRAIN_FLAG_MU);
        refit_cost:                     0;
        track_type:                     ELRL;
        ai_special_flag:                AI_FLAG_PASSENGER;
        power:                          825 kW;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        dual_headed:                    0;
        cargo_capacity:                 115;
        weight:                         75 ton;
        ai_engine_rank:                 0;
        engine_class:                   ENGINE_CLASS_DIESEL;
        extra_power_per_wagon:          0;
        tractive_effort_coefficient:    0.3;
        air_drag_coefficient:           0.06;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 3, DISABLE_WAGON_POWER);
        extra_weight_per_wagon:         0;
        bitmask_vehicle_info:           0;
    }
    graphics {
        purchase:                       spriteset_purchase_Metroliner_I;
        additional_text:                return string(STR_Metroliner_I_Desc);
        articulated_part:               sw_Metroliner_I_articulated_part;
        length:                         sw_Metroliner_I_length;
        cargo_capacity:                 sw_Metroliner_I_capacity;
        cargo_subtype_text:             sw_Metroliner_I_livery;
        default:                        sw_Metroliner_I_grapfics;
    }
}
