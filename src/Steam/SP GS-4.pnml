//--------------------------------------------------------------------------------------------------SP GS-4
spriteset (spriteset_purchase_GS4, "gfx/USA_GS-4_Daylight.png") { [4,  97,  87,  14,  -27,  -10] }

template tmpl_GS4_Locomotive_1(x,y) {
    [x,      y,    8,     24,     -3,    -10]
    [x+9,    y,    22,    20,     -14,   -10]
    [x+32,   y,    32,    16,     -16,   -12]
    [x+65,   y,    22,    20,     -8,    -12]
}

template tmpl_GS4_Locomotive_2(x,y) {
    [x,      y,    8,     24,     -3,    -10]
    [x+9,    y,    22,    20,     -14,   -11]
    [x+32,   y,    32,    16,     -16,   -12]
    [x+65,   y,    22,    20,     -8,    -11]
}

template tmpl_GS4_Tender_1(x,y) {
    [x,      y,    8,     24,     -3,    -12]
    [x+9,    y,    22,    20,     -12,   -11]
    [x+32,   y,    32,    16,     -16,   -12]
    [x+65,   y,    22,    20,     -6,    -10]
}

template tmpl_GS4_Tender_2(x,y) {
    [x,      y,    8,     24,     -3,    -6]
    [x+9,    y,    22,    20,     -16,   -9]
    [x+32,   y,    32,    16,     -24,   -12]
    [x+65,   y,    22,    20,     -10,   -12]
}

spriteset (spriteset_GS4_Locomotive_Daylight, "gfx/USA_GS-4_Daylight.png") { tmpl_GS4_Locomotive_1(4,4)   tmpl_GS4_Locomotive_2(92,4) }
spriteset (spriteset_GS4_Locomotive_Daylight_second, "gfx/USA_GS-4_Daylight.png") { tmpl_GS4_Locomotive_1(4,35)   tmpl_GS4_Locomotive_2(92,35) }
spriteset (spriteset_GS4_Tender_Daylight, "gfx/USA_GS-4_Daylight.png") { tmpl_GS4_Tender_1(4,66) tmpl_GS4_Tender_2(92,66) }

switch(FEAT_TRAINS,SELF,sw_GS4_articulated_part, extra_callback_info1) {
    1:  return item_GS4;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch(FEAT_TRAINS, SELF, sw_GS4_Locomotive_animation, motion_counter % 4) {
    0..1:  spriteset_GS4_Locomotive_Daylight;
    spriteset_GS4_Locomotive_Daylight_second;
}
switch(FEAT_TRAINS, SELF, sw_GS4_grapfics, position_in_vehid_chain % 2) {
    0:  sw_GS4_Locomotive_animation;
    spriteset_GS4_Tender_Daylight;
}
switch(FEAT_TRAINS, SELF, sw_GS4_length, position_in_vehid_chain % 2) {
    0:  8;
    6;
}
switch(FEAT_TRAINS, SELF, sw_GS4_effect, position_in_vehid_chain % 2) {
    0:  visual_effect_and_powered(VISUAL_EFFECT_STEAM, -2, DISABLE_WAGON_POWER);
    visual_effect_and_powered(VISUAL_EFFECT_DISABLE, -2, DISABLE_WAGON_POWER);
}

item(FEAT_TRAINS, item_GS4, 19) {
    property {
        name:                           string(STR_GS4_Name);
        climates_available:             bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
        introduction_date:              date(1941, 4, 1);
        model_life:                     20;
        vehicle_life:                   30;
        reliability_decay:              15;
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   bitmask();
        loading_speed:                  0;
        cost_factor:                    48;
        running_cost_factor:            148;
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        speed:                          110 mph;
        misc_flags:                     bitmask(TRAIN_FLAG_MU);
        refit_cost:                     0;
        track_type:                     RAIL;
        ai_special_flag:                AI_FLAG_PASSENGER;
        power:                          2250 hp;
        running_cost_base:              RUNNING_COST_STEAM;
        dual_headed:                    0;
        cargo_capacity:                 1;
        weight:                         358 ton;
        ai_engine_rank:                 0;
        engine_class:                   ENGINE_CLASS_STEAM;
        extra_power_per_wagon:          0;
        tractive_effort_coefficient:    0.08;
        air_drag_coefficient:           8/180;
        visual_effect_and_powered:      visual_effect_and_powered(VISUAL_EFFECT_STEAM, -7, DISABLE_WAGON_POWER);
        extra_weight_per_wagon:         0;
        bitmask_vehicle_info:           0;
    }
    graphics {
        purchase:                       spriteset_purchase_GS4;
        additional_text:                return string(STR_GS4_Desc);
        articulated_part:               sw_GS4_articulated_part;
        length:                         sw_GS4_length;
        cargo_capacity:                 0;
        visual_effect_and_powered:      sw_GS4_effect;
        default:                        sw_GS4_grapfics;
    }
}
